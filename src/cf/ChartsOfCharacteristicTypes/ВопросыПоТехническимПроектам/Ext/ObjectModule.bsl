#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения = Неопределено Тогда
		
		ТипЗначения = Новый ОписаниеТипов("Строка");
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если НЕ ДанныеЗаполнения.Свойство("ТипЗначения") Тогда
			ТипЗначения = Новый ОписаниеТипов("Строка");
		КонецЕсли;
		
		Если ЭтоНовый() Тогда
			Если ДанныеЗаполнения.Свойство("Проект") Тогда
				Проект = ДанныеЗаполнения.Проект;
			КонецЕсли;
		Конецесли;
		
	КонецЕсли;
		
	Если ЭтоНовый() И НЕ ЗначениеЗаполнено(Проект) Тогда
		Проект = Проекты.ПроектПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ЭтоНовый() И НЕ ЗначениеЗаполнено(Код) Тогда
		УстановитьНовыйКодСУчетомПроекта();
	КонецЕсли;
	
	Если Родитель = Неопределено ИЛИ Родитель.Пустая() Тогда
		ПолныйКод = Код;
	Иначе
		ПолныйКод = ПолныйКодРодителя(Родитель) + Строка(Код);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолныйКодРодителя(ГруппаРодитель)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВопросыПоТехническимПроектам.ПолныйКод КАК ПолныйКод
	|ИЗ
	|	ПланВидовХарактеристик.ВопросыПоТехническимПроектам КАК ВопросыПоТехническимПроектам
	|ГДЕ
	|	ВопросыПоТехническимПроектам.Ссылка = &ГруппаРодитель"
	;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ГруппаРодитель", ГруппаРодитель);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПолныйКод = "";
	
	Если Выборка.Следующий() Тогда
		ПолныйКод = Выборка.ПолныйКод;
	КонецЕсли;
	
	Возврат Строка(ПолныйКод);
	
КонецФункции

Процедура УстановитьНовыйКодСУчетомПроекта()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1 
	|	Вопросы.Код КАК Код
	|ИЗ
	|	ПланВидовХарактеристик.ВопросыПоТехническимПроектам КАК Вопросы
	|ГДЕ
	|	Вопросы.Родитель = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыПоТехническимПроектам.ПустаяСсылка)
	|   И Вопросы.Проект = &Проект
	|
	|УПОРЯДОЧИТЬ ПО
	|	Код УБЫВ"
	;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		НовыйКод = Число(Выборка.Код) + 1;
	Иначе
		НовыйКод = 1;
	КонецЕсли;
	
    Код = Формат(НовыйКод, "ЧЦ=2; ЧДЦ=0; ЧВН=");
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли