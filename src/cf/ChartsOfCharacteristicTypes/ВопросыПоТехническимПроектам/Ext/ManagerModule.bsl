#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Функция возвращает массив актуальных вопросоп
// Элементами массива является структура, содержащая реквизиты вопроса
//
Функция СписокАктуальныхВопросов(Проект) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВопросыПоТехническимПроектам.Ссылка КАК Вопрос,
	|	ВопросыПоТехническимПроектам.ТипЗначения КАК ТипЗначения,
	|	ВопросыПоТехническимПроектам.КонтрольнаяТочкаТехническогоПроекта КАК КонтрольнаяТочка,
	|	ВопросыПоТехническимПроектам.ПолныйКод КАК ПолныйКод
	|ИЗ
	|	ПланВидовХарактеристик.ВопросыПоТехническимПроектам КАК ВопросыПоТехническимПроектам
	|ГДЕ
	|	НЕ ВопросыПоТехническимПроектам.ПометкаУдаления
	|	И НЕ ВопросыПоТехническимПроектам.ЭтоГруппа
	|	И ВопросыПоТехническимПроектам.Актуален
	|	И ВопросыПоТехническимПроектам.Проект = &Проект
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПолныйКод"
	;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Проект", Проект);
	
	МассивВопросов = Новый Массив();
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтруктураРеквизитов = Новый Структура("Вопрос,ТипЗначения,КонтрольнаяТочка,ПолныйКод");
		ЗаполнитьЗначенияСвойств(СтруктураРеквизитов, Выборка);
		МассивВопросов.Добавить(СтруктураРеквизитов);
	КонецЦикла;
	
	Возврат МассивВопросов;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Проект)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли