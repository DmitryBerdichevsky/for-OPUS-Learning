
#Область ПрограммныйИнтерфейс

// Запускает тестирование веток проекта.
//
Процедура СформироватьРасписаниеИЗапуститьТестирование() Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	ПроектыКОбработке = ПроектыКОбработке();
	
	Для Каждого Проект Из ПроектыКОбработке Цикл
		ЗаполнитьРасписание(Проект.Ссылка);
		ЗапуститьОтложенныеЗапускиТестирования(Проект.Ссылка);
	КонецЦикла; 
	
КонецПроцедуры 

//Создает запланированные запуски тестирования по проекту на указанное время.
//
// Параметры:
//  Проект                - СправочникСсылка.Проекты  - Проект, для которого формируется расписание.
//  ТекущаяДата           - Дата                      - Дата, на которую формируется расписание. 
//
Процедура ЗаполнитьРасписание(Проект, ТекущаяДата=Неопределено) Экспорт
	
	// Формирует и доформировывает расписание запусков.
	Если ТекущаяДата = Неопределено Тогда
		ТекущаяДата = ТекущаяДата();
	КонецЕсли;
	
	ДоступноеВремя = ДоступноеВремяИМощностьДляТестирования(Проект, ТекущаяДата);
	Если ДоступноеВремя.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	ЗапускиДляТестирования = ЗапланированныеЗапускиТестирования(Проект, ДоступноеВремя);
	ДатаНачала = ВремяНачалаТестированияПоДате(Проект, ДоступноеВремя[0].ДатаВремяЗапуска);

	// Считываются ветки для тестирования
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Ветки.Ссылка КАК Ветка,
	|	Ветки.Имя КАК ВеткаИмя,
	|	Ветки.Ответственный КАК Ответственный,
	|	Ветки.НастройкаЗапускаТестирования КАК НастройкаЗапускаТестирования,
	|	Ветки.НастройкаЗапускаТестирования.ПредпочтительноеВремяЗапуска КАК ПредпочтительноеВремяЗапуска,
	|	Ветки.НастройкаЗапускаТестирования.ЗанимаемаяМощность КАК ЗанимаемаяМощность,
	|	Ветки.Приемник.Ответственный КАК ПриемникОтветственный,
	|	Ветки.ПриоритетТестирования КАК ПриоритетТестирования,
	|	ПоследнийЗапускТестирования.Ссылка КАК ПоследнийЗапуск,
	|	ПоследнийЗапускТестирования.Pipeline_ID КАК ИдентификаторПоследнегоЗапуска,
	|	ЕСТЬNULL(ПоследнийЗапускТестирования.Дата, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ДатаПоследнегоЗапуска,
	|	ВЫБОР
	|		КОГДА Ошибки.Ссылка ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВсеОшибкиИсправлены,
	|	ВЫБОР
	|		КОГДА Ветки.ПриоритетТестирования = ЗНАЧЕНИЕ(Перечисление.ПриоритетыТестированияВеток.Низкий)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЗапускатьТестированиеЕслиЕстьНеисправленныеОшибки,
	|	ВЫБОР
	|		КОГДА Ветки.ПриоритетТестирования = ЗНАЧЕНИЕ(Перечисление.ПриоритетыТестированияВеток.Высокий)
	|			ТОГДА 1
	|		КОГДА Ветки.ПриоритетТестирования = ЗНАЧЕНИЕ(Перечисление.ПриоритетыТестированияВеток.Средний)
	|				И Ошибки.Ссылка ЕСТЬ NULL
	|			ТОГДА 2
	|		КОГДА Ветки.ПриоритетТестирования = ЗНАЧЕНИЕ(Перечисление.ПриоритетыТестированияВеток.Низкий)
	|				И Ошибки.Ссылка ЕСТЬ NULL
	|			ТОГДА 3
	|		КОГДА Ветки.ПриоритетТестирования = ЗНАЧЕНИЕ(Перечисление.ПриоритетыТестированияВеток.Средний)
	|			ТОГДА 4
	|		ИНАЧЕ 5
	|	КОНЕЦ КАК ПорядокПоПриоритету,
	|	ВЫБОР
	|		КОГДА Ветки.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыВеток.ОсновнаяВеткаПроекта)
	|			ТОГДА 1
	|		КОГДА Ветки.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыВеток.ВеткаВерсии)
	|			ТОГДА 2
	|		КОГДА Ветки.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыВеток.ВеткаДляИсправленияОшибок)
	|			ТОГДА 3
	|		КОГДА Ветки.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыВеток.ВеткаТехническогоПроекта)
	|			ТОГДА 4
	|		ИНАЧЕ 5
	|	КОНЕЦ КАК ПорядокПоТипу
	|ИЗ
	|	Справочник.Ветки КАК Ветки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗапускТестирования КАК ПоследнийЗапускТестирования
	|		ПО (ПоследнийЗапускТестирования.Ссылка В
	|				(ВЫБРАТЬ ПЕРВЫЕ 1
	|					ЗапускТестирования.Ссылка
	|				ИЗ
	|					Документ.ЗапускТестирования КАК ЗапускТестирования
	|				ГДЕ
	|					ЗапускТестирования.Ветка = Ветки.Ссылка
	|					И ЗапускТестирования.Проведен
	|					И НЕ ЗапускТестирования.ПометкаУдаления
	|					И НЕ ПОДСТРОКА(ЗапускТестирования.Pipeline_ID, 1, 1) ПОДОБНО """"
	|					И ЗапускТестирования.Настройка <> ЗНАЧЕНИЕ(Справочник.НастройкиЗапускаТестирования.ПустаяСсылка)
	|				УПОРЯДОЧИТЬ ПО
	|					ЗапускТестирования.Дата УБЫВ))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Ошибки КАК Ошибки
	|		ПО (Ветки.Тип В (ЗНАЧЕНИЕ(Перечисление.ТипыВеток.ВеткаТехническогоПроекта), ЗНАЧЕНИЕ(Перечисление.ТипыВеток.ВеткаДляИсправленияОшибок)))
	|			И (ИСТИНА В
	|					(ВЫБРАТЬ ПЕРВЫЕ 1
	|						ИСТИНА
	|					ИЗ
	|						Справочник.Ошибки.ИсправлениеВВетках КАК ОшибкиИсправлениеВВетках
	|					ГДЕ
	|						ОшибкиИсправлениеВВетках.Ссылка.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыОшибок.Зарегистрирована), ЗНАЧЕНИЕ(Перечисление.СтатусыОшибок.Признана))
	|						И ОшибкиИсправлениеВВетках.СостояниеИсправления = ЗНАЧЕНИЕ(Перечисление.СостоянияИсправленияОшибок.ТребуетсяИсправление)
	|						И ОшибкиИсправлениеВВетках.Ссылка.Исправляется = ЗНАЧЕНИЕ(Перечисление.ВариантыИсправленияОшибок.ВРазныхветках)
	|						И ОшибкиИсправлениеВВетках.ВеткаИсправления = Ветки.Ссылка)
	|				ИЛИ ИСТИНА В
	|					(ВЫБРАТЬ ПЕРВЫЕ 1
	|						ИСТИНА
	|					ИЗ
	|						Справочник.Ошибки КАК ОшибкиОбнаруженныеВВетке
	|					ГДЕ
	|						ОшибкиОбнаруженныеВВетке.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыОшибок.Зарегистрирована), ЗНАЧЕНИЕ(Перечисление.СтатусыОшибок.Признана))
	|						И ОшибкиОбнаруженныеВВетке.Исправляется = ЗНАЧЕНИЕ(Перечисление.ВариантыИсправленияОшибок.ТолькоВВеткеОбнаружения)
	|						И ОшибкиОбнаруженныеВВетке.ВеткаОбнаружения = Ветки.Ссылка))
	|ГДЕ
	|	Ветки.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыВеток.Тестируется)
	|	И Ветки.Владелец = &Проект
	|	И НЕ Ветки.ПометкаУдаления
	|	И НЕ Ветки.Ссылка В (&ВеткиНаТестировании)
	|	И (Ветки.НастройкаЗапускаТестирования = ЗНАЧЕНИЕ(Справочник.НастройкиЗапускаТестирования.ПустаяСсылка)
	|			ИЛИ &ТекущаяДата = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ИЛИ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&ДатаНачала, ДЕНЬ), ЧАС, ЧАС(Ветки.НастройкаЗапускаТестирования.ПредпочтительноеВремяЗапуска)) <= &ТекущаяДата)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокПоПриоритету,
	|	ПорядокПоТипу,
	|	ДатаПоследнегоЗапуска";

	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата); 
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ВеткиНаТестировании", ЗапускиДляТестирования.ВыгрузитьКолонку("Ветка"));
	Запрос.УстановитьПараметр("Проект", Проект);
	
	ВыборкаВетокДляЗапуска = Запрос.Выполнить().Выбрать();
	АвторСообщения = Пользователи.ТекущийПользователь();
	
	ТекущаяДата = ТекущаяДата();
	НачалоЧаса = НачалоДня(ТекущаяДата) + Час(ТекущаяДата) * 60*60;
	
	ИндексТекущейСтрокиПоУмолчанию = 0;
	Пока ИндексТекущейСтрокиПоУмолчанию < ДоступноеВремя.Количество() И ДоступноеВремя[ИндексТекущейСтрокиПоУмолчанию].ДатаВремяЗапуска < НачалоЧаса Цикл
		ИндексТекущейСтрокиПоУмолчанию = ИндексТекущейСтрокиПоУмолчанию + 1;
	КонецЦикла;
	
	Пока ВыборкаВетокДляЗапуска.Следующий() Цикл
		// Проверки, можно ли запускать тестирование.
		
		ШаблонВеткаНеТестируется = НСтр("ru='не поставлена на тестирование'");
		СообщениеВеткаНеТестируется = СтрШаблон(НСтр("ru='Ветка %1 не поставлена на тестирование.'"), ВыборкаВетокДляЗапуска.ВеткаИмя) + Символы.ПС;

		// Проверка, заполнена ли настройка тестирования.
		Если НЕ ЗначениеЗаполнено(ВыборкаВетокДляЗапуска.НастройкаЗапускаТестирования) Тогда
			ТекстСообщения = СообщениеВеткаНеТестируется + НСтр("ru='В ветке не указана настройка запуска тестирования.'");
			ОтветственныйДляСообщения = ?(ЗначениеЗаполнено(ВыборкаВетокДляЗапуска.ПриемникОтветственный), 
															ВыборкаВетокДляЗапуска.ПриемникОтветственный, 
															ВыборкаВетокДляЗапуска.Ответственный);
			ДобавитьСообщениеПоОбъекту(ВыборкаВетокДляЗапуска.Ветка, 
									   ОтветственныйДляСообщения,
									   АвторСообщения,
									   ДатаНачала,
									   ТекстСообщения,
									   ШаблонВеткаНеТестируется);
			Продолжить;
		КонецЕсли;
		
		// Проверка, существует ли ветка на сервере.
		Если НЕ ВеткаСуществуетНаСервере(Проект, ВыборкаВетокДляЗапуска.ВеткаИмя) Тогда
			ТекстСообщения = СообщениеВеткаНеТестируется 
								+ СтрШаблон(НСтр("ru='Ветка с именем %1 не существует на сервере.'"), ВыборкаВетокДляЗапуска.ВеткаИмя);
			ДобавитьСообщениеПоОбъекту(ВыборкаВетокДляЗапуска.Ветка, 
									   ВыборкаВетокДляЗапуска.Ответственный,
									   АвторСообщения,
									   ДатаНачала,
									   ТекстСообщения,
									   ШаблонВеткаНеТестируется);
			Продолжить;
		КонецЕсли;

		// Проверка, есть ли изменения в ветке с последнего запуска.
		Если НЕ ЕстьИзмененияВВетке(Проект, ВыборкаВетокДляЗапуска.ВеткаИмя, ВыборкаВетокДляЗапуска.ИдентификаторПоследнегоЗапуска) Тогда
			ТекстСообщения = СообщениеВеткаНеТестируется 
							+ СтрШаблон(НСтр("ru='В ветке нет изменений с последнего запуска %1.'"), ВыборкаВетокДляЗапуска.ИдентификаторПоследнегоЗапуска);
			ДобавитьСообщениеПоОбъекту(ВыборкаВетокДляЗапуска.Ветка, 
									   ВыборкаВетокДляЗапуска.Ответственный,
									   АвторСообщения,
									   ДатаНачала,
									   ТекстСообщения,
									   ШаблонВеткаНеТестируется);
			Продолжить;
		КонецЕсли;
		
		МощностьДляЗапуска = ВыборкаВетокДляЗапуска.ЗанимаемаяМощность;
		ДатаЗапуска = Неопределено;
		
		ЗапускатьТестирование = ВыборкаВетокДляЗапуска.ВсеОшибкиИсправлены ИЛИ ВыборкаВетокДляЗапуска.ЗапускатьТестированиеЕслиЕстьНеисправленныеОшибки;
		
		ПредпочтительноеДатаВремяЗапуска = Неопределено;
		Если ЗначениеЗаполнено(ВыборкаВетокДляЗапуска.ПредпочтительноеВремяЗапуска) Тогда
			ПредпочтительноеДатаВремяЗапуска = НачалоДня(ТекущаяДата) + Час(ВыборкаВетокДляЗапуска.ПредпочтительноеВремяЗапуска) * 60*60; 
		КонецЕсли;
		
		Если ЗапускатьТестирование Тогда
			ИндексТекущейСтроки = ИндексТекущейСтрокиПоУмолчанию;
			
			Пока МощностьДляЗапуска > 0 И ИндексТекущейСтроки < ДоступноеВремя.Количество() Цикл
				
				Если ПредпочтительноеДатаВремяЗапуска <> Неопределено
					И ДоступноеВремя[ИндексТекущейСтроки].ДатаВремяЗапуска < ПредпочтительноеДатаВремяЗапуска Тогда
					ИндексТекущейСтроки = ИндексТекущейСтроки + 1;
					Продолжить;
				КонецЕсли;
				
				Если ДоступноеВремя[ИндексТекущейСтроки].СвободнаяМощность <> 0 Тогда 
					Если ДатаЗапуска = Неопределено Тогда
						ДатаЗапуска = ДоступноеВремя[ИндексТекущейСтроки].ДатаВремяЗапуска;
					КонецЕсли;
					Если МощностьДляЗапуска <= ДоступноеВремя[ИндексТекущейСтроки].СвободнаяМощность Тогда
						ДоступноеВремя[ИндексТекущейСтроки].СвободнаяМощность = ДоступноеВремя[ИндексТекущейСтроки].СвободнаяМощность - МощностьДляЗапуска;
						МощностьДляЗапуска = 0;
					Иначе
						МощностьДляЗапуска = МощностьДляЗапуска - ДоступноеВремя[ИндексТекущейСтроки].СвободнаяМощность;
						ДоступноеВремя[ИндексТекущейСтроки].СвободнаяМощность = 0;
					КонецЕсли;
				КонецЕсли;
				
				Если ДоступноеВремя[ИндексТекущейСтроки].СвободнаяМощность = 0 Тогда
					ИндексТекущейСтроки = ИндексТекущейСтроки + 1;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		БудетСозданЗапуск = МощностьДляЗапуска = 0 И ДатаЗапуска <> Неопределено;

		Если БудетСозданЗапуск Тогда

			ЗапускТестирования = Документы.ЗапускТестирования.СоздатьДокумент();
			ЗапускТестирования.Автор = Пользователи.ТекущийПользователь();
			ЗапускТестирования.Ветка = ВыборкаВетокДляЗапуска.Ветка;
			ЗапускТестирования.Проект = Проект;
			ЗапускТестирования.Дата = ДатаЗапуска;
			ЗапускТестирования.Настройка = ВыборкаВетокДляЗапуска.НастройкаЗапускаТестирования;
			ЗапускТестирования.Записать();
			
		Иначе

			Если ВыборкаВетокДляЗапуска.ВсеОшибкиИсправлены Тогда
				ТекстСообщения = СообщениеВеткаНеТестируется + НСтр("ru='Не хватило слотов на тестирование.'");
			Иначе
				ТекстСообщения = СообщениеВеткаНеТестируется + НСтр("ru='Есть неисправленные ошибки.'");
			КонецЕсли;
				
			ДобавитьСообщениеПоОбъекту(ВыборкаВетокДляЗапуска.Ветка, 
										 ВыборкаВетокДляЗапуска.Ответственный,
										 АвторСообщения,
										 ДатаНачала,
										 ТекстСообщения,
										 ШаблонВеткаНеТестируется);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

//Возвращает таблицу с доступным временем и мощностями для тестирования.
//
// Параметры:
//  Проект                - СправочникСсылка.Проекты  - Проект, для которого формируется расписание.
//  ТекущаяДата           - Дата                      - Дата, на которую формируется расписание. 
//
// Возвращаемое значение:
//   ТаблицаЗначений - Доступное время и мощность для тестирования.
//       * ДатаВремяЗапуска   - Дата.
//       * ДоступнаяМощность  - Число.
//       * СвободнаяМощность  - Число.
Функция ДоступноеВремяИМощностьДляТестирования(Проект, ТекущаяДата=Неопределено) Экспорт
	
	// Определяется дата и время начала и окончания периода, для которого нужно получить мощность.
	Если ТекущаяДата = Неопределено Тогда
		ТекущаяДата = ТекущаяДата();
	КонецЕсли;
	ДатаНачала = ВремяНачалаТестированияПоДате(Проект, ТекущаяДата);
	ДатаОкончания = ВремяОкончанияТестированияПоДате(Проект, ДатаНачала);
	НомерДняНедели = ДеньНедели(ДатаНачала);
	
	// Заполняется таблица с доступной мощностью на период для формирования расписания.
	
	ДоступноеВремя = Новый ТаблицаЗначений; 
	ДоступноеВремя.Колонки.Добавить("ДатаВремяЗапуска");
	ДоступноеВремя.Колонки.Добавить("ДоступнаяМощность");
	ДоступноеВремя.Колонки.Добавить("СвободнаяМощность");
	
	Для Каждого Строка Из Проект.СлотыТестирования Цикл
		
		Если Строка.ДоступнаяМощность = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		РазницаВДнях = Строка.НомерДня - НомерДняНедели;
		Если РазницаВДнях < 0 Тогда
			РазницаВДнях = РазницаВДнях + 7;
		КонецЕсли;
		
		ДатаВремяСтроки = НачалоДня(ТекущаяДата) + Строка.НомерЧаса*60*60 + РазницаВДнях*24*60*60;
		Если ДатаВремяСтроки >= ДатаНачала И ДатаВремяСтроки < ДатаОкончания Тогда
			НоваяСтрока = ДоступноеВремя.Добавить();
			НоваяСтрока.ДоступнаяМощность = Строка.ДоступнаяМощность;
			НоваяСтрока.СвободнаяМощность = Строка.ДоступнаяМощность;
			НоваяСтрока.ДатаВремяЗапуска = ДатаВремяСтроки;
		КонецЕсли;
		
	КонецЦикла;
	
	ДоступноеВремя.Сортировать("ДатаВремяЗапуска");
	Возврат ДоступноеВремя;
	
КонецФункции

//Возвращает запланированные запуски тестирования по проекту.
//
// Параметры:
//  Проект                - СправочникСсылка.Проекты - Проект, для которого ищутся запуски тестирования.
//  ДоступноеВремя        - ТаблицаЗначений - Доступное время и мощность для тестирования.
//       * ДатаВремяЗапуска   - Дата.
//       * ДоступнаяМощность  - Число.
//       * СвободнаяМощность  - Число.
// 
// Возвращаемое значение:
//   ТаблицаЗначений - Запланированные запуски тестирования.
//       * ЗапускТестирования               - Документ.ЗапускТестирования.
//       * Ветка                            - Справочник.Ветки.
//       * ДатаВремяЗапуска                 - Дата.
//       * ИдентификаторЗапускаТестирования - Строка.
//       * Запущен                          - Булево.
//       * НастройкаЗапуска                 - Справочник.НастройкиЗапускаТестирования.
//       * ЗанимаемаяМощность               - Число.
//
Функция ЗапланированныеЗапускиТестирования(Проект, ДоступноеВремя=Неопределено) Экспорт
	Если ДоступноеВремя = Неопределено Тогда
		ДоступноеВремя = ДоступноеВремяИМощностьДляТестирования(Проект);
	КонецЕсли;
	Если ДоступноеВремя.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДатаНачала = ВремяНачалаТестированияПоДате(Проект, ДоступноеВремя[0].ДатаВремяЗапуска);
	ДатаОкончания = ВремяОкончанияТестированияПоДате(Проект, ДатаНачала);
	
	// Считываются уже запланированные запуски тестирования на доступное время, чтобы определить оставшиеся ресурсы.
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗапускТестирования.Ссылка КАК ЗапускТестирования,
	|	ЗапускТестирования.Номер КАК Номер,
	|	ЗапускТестирования.Ветка КАК Ветка,
	|	ЗапускТестирования.Дата КАК ДатаВремяЗапуска,
	|	ЗапускТестирования.Pipeline_ID КАК ИдентификаторЗапускаТестирования,
	|	ЗапускТестирования.Проведен КАК Запущен,
	|	ЗапускТестирования.Настройка КАК НастройкаЗапуска,
	|	ЗапускТестирования.Настройка.ЗанимаемаяМощность КАК ЗанимаемаяМощность,
	|	0 КАК ДоступнаяМощность
	|ИЗ
	|	Документ.ЗапускТестирования КАК ЗапускТестирования
	|ГДЕ
	|	ЗапускТестирования.Настройка <> ЗНАЧЕНИЕ(Справочник.НастройкиЗапускаТестирования.ПустаяСсылка)
	|	И ЗапускТестирования.Проект = &Проект
	|	И НЕ ЗапускТестирования.ПометкаУдаления
	|	И ЗапускТестирования.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗапускТестирования.Дата";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("Проект", Проект);
	ЗапланированныеЗапуски = Запрос.Выполнить().Выгрузить();
	ИндексТекущейСтроки = 0;
	
	Для Каждого ЗапланированныйЗапуск Из ЗапланированныеЗапуски Цикл
		Дата = ЗапланированныйЗапуск.ДатаВремяЗапуска;
		ДатаЧас = НачалоДня(Дата) + Час(Дата)*60*60;
		Пока ИндексТекущейСтроки < ДоступноеВремя.Количество() 
			И (ДоступноеВремя[ИндексТекущейСтроки].ДатаВремяЗапуска < ДатаЧас 
			ИЛИ ДоступноеВремя[ИндексТекущейСтроки].СвободнаяМощность = 0) Цикл
			ИндексТекущейСтроки = ИндексТекущейСтроки + 1;
		КонецЦикла;
		Если ИндексТекущейСтроки >= ДоступноеВремя.Количество() Тогда
			Прервать;
		КонецЕсли;
		Если ДоступноеВремя[ИндексТекущейСтроки].ДатаВремяЗапуска > ДатаЧас + 60*60 Тогда
			Продолжить;
		КонецЕсли;
		
		МощностьДляЗапуска = ЗапланированныйЗапуск.ЗанимаемаяМощность;
		ЗапланированныйЗапуск.ДоступнаяМощность = МощностьДляЗапуска;
		Пока МощностьДляЗапуска > 0 И ИндексТекущейСтроки < ДоступноеВремя.Количество() Цикл
			Если МощностьДляЗапуска <= ДоступноеВремя[ИндексТекущейСтроки].СвободнаяМощность Тогда
				ДоступноеВремя[ИндексТекущейСтроки].СвободнаяМощность = ДоступноеВремя[ИндексТекущейСтроки].СвободнаяМощность - МощностьДляЗапуска;
				МощностьДляЗапуска = 0;
			Иначе
				МощностьДляЗапуска = МощностьДляЗапуска - ДоступноеВремя[ИндексТекущейСтроки].СвободнаяМощность;
				ДоступноеВремя[ИндексТекущейСтроки].СвободнаяМощность = 0;
				ИндексТекущейСтроки = ИндексТекущейСтроки + 1;
			КонецЕсли;
		КонецЦикла;
		ЗапланированныйЗапуск.ДоступнаяМощность = ЗапланированныйЗапуск.ДоступнаяМощность - МощностьДляЗапуска;
	КонецЦикла;
	
	Возврат ЗапланированныеЗапуски;

КонецФункции

//Возвращает отмененные запуски тестирования по проекту.
//
// Параметры:
//  Проект                - СправочникСсылка.Проекты - Проект, для которого ищутся запуски тестирования.
//  ДоступноеВремя        - ТаблицаЗначений - Доступное время и мощность для тестирования.
//       * ДатаВремяЗапуска   - Дата.
//       * ДоступнаяМощность  - Число.
//       * СвободнаяМощность  - Число.
// 
// Возвращаемое значение:
//   ТаблицаЗначений - Запланированные запуски тестирования.
//       * ЗапускТестирования - Документ.ЗапускТестирования.
//       * Ветка              - Справочник.Ветки.
//
Функция ОтмененныеЗапускиТестирования(Проект, ДоступноеВремя=Неопределено) Экспорт
	Если ДоступноеВремя = Неопределено Тогда
		ДоступноеВремя = ДоступноеВремяИМощностьДляТестирования(Проект);
	КонецЕсли;
	Если ДоступноеВремя.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДатаНачала = ВремяНачалаТестированияПоДате(Проект, ДоступноеВремя[0].ДатаВремяЗапуска);
	ДатаОкончания = ВремяОкончанияТестированияПоДате(Проект, ДатаНачала);
	
	// Считываются уже запланированные запуски тестирования на доступное время, чтобы определить оставшиеся ресурсы.
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗапускТестирования.Ссылка КАК ЗапускТестирования,
	|	ЗапускТестирования.Ветка КАК Ветка,
	|	ЗапускТестирования.Дата КАК ДатаВремяЗапуска
	|ИЗ
	|	Документ.ЗапускТестирования КАК ЗапускТестирования
	|ГДЕ
	|	ЗапускТестирования.Настройка = ЗНАЧЕНИЕ(Справочник.НастройкиЗапускаТестирования.ПустаяСсылка)
	|	И НЕ ЗапускТестирования.Проведен
	|	И НЕ ЗапускТестирования.ЗапуститьТестыВыполненныеСОшибкой
	|	И ЗапускТестирования.Проект = &Проект
	|	И НЕ ЗапускТестирования.ПометкаУдаления
	|	И ЗапускТестирования.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗапускТестирования.Дата";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("Проект", Проект);
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

//Проверяет, есть ли в ветке изменения после указанного запуска тестирования.
//
// Параметры:
//  Проект                - СправочникСсылка.Проекты - Владелец ветки.
//  ИмяВетки              - Строка - Имя ветки. 
//  ИдентификаторЗапуска  - Строка - Идентификатор запуска тестирования. 
// 
// Возвращаемое значение:
//  Булево - Есть или нет изменения в ветке после указанного запуска тестирования.
//
Функция ЕстьИзмененияВВетке(Проект, ИмяВетки, ИдентификаторЗапуска) Экспорт

	Если НЕ ЗначениеЗаполнено(ИдентификаторЗапуска) Тогда
		Возврат Истина;
	КонецЕсли;

	Попытка
		КоммитВетки = ИнформацияОВетке(Проект, ИмяВетки)["commit"]["id"];
		КоммитЗапуска = ИнформацияОЗапуске(Проект, ИдентификаторЗапуска)["sha"];
		Ответ = РазницаМеждуВетками(Проект, КоммитЗапуска, КоммитВетки);
		ЕстьРазличия = ЕстьРазличияВКодеКонфигурации(Ответ["diffs"],
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Проект, "ПутьКПроектуВРепозитории"));
		ИнформацияОКоммитеВетки = ИнформацияОКоммите(Проект, КоммитВетки);
		БылиКонфликты = СтрНайти(ИнформацияОКоммитеВетки["message"], "Conflicts") <> 0;
		ПоследнийКоммитЭтоМердж = ИнформацияОКоммитеВетки["parent_ids"].Найти(КоммитЗапуска) <> Неопределено
									И ИнформацияОКоммитеВетки["parent_ids"].Количество() > 1;
		Если ПоследнийКоммитЭтоМердж Тогда
			Возврат (ПоследнийКоммитЭтоМердж И БылиКонфликты) ИЛИ ЕстьРазличия;
		Иначе
			Возврат ЕстьРазличия;
		КонецЕсли;
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Запуск тестирования.Получение изменений по ветке'"),
										УровеньЖурналаРегистрации.Ошибка,,,ОписаниеОшибки());

		Возврат Истина;
	КонецПопытки;
	
КонецФункции

//Проверяет, существует ли ветка на сервере.
//
// Параметры:
//  Проект                - СправочникСсылка.Проекты - Владелец ветки.
//  ИмяВетки              - Строка - Имя ветки. 
// 
// Возвращаемое значение:
//  Булево - Есть или нет ветка с таким именем на сервере.
//
Функция ВеткаСуществуетНаСервере(Проект, ИмяВетки) Экспорт
	
	Соединение = GitLabСоединение(Проект.РезультатыВыполненияТестовАдресСервера);
	ВеткаСуществует = Истина;
	Попытка
		КоммитВетки = ИнформацияОВетке(Проект, ИмяВетки);
	Исключение
		ВеткаСуществует = Ложь;
	КонецПопытки;
	Соединение = Неопределено;
	Возврат ВеткаСуществует;
	
КонецФункции

//Запускает тестирование на сервере.
//
// Параметры:
//  Проект                - СправочникСсылка.Проекты - Проект, для которого создается запуск.
//  ИмяВетки              - Строка - Имя ветки на сервере тестирования. 
//  Параметры             - Структура - Параметры, которые передаются созданному запуску. 
// 
// Возвращаемое значение:
//  Структура - 
//     * ЗапросВыполненУспешно  - Булево - Истина, если запуск успешно был создан и Ложь если нет;
//     * Ответ                  - Строка - Созданный запуск или текст исключения;
//   
Функция ЗапуститьТестирование(Проект, ИмяВетки, Параметры=Неопределено) Экспорт
	Если Параметры = Неопределено Тогда
		Параметры = Новый Структура;
	КонецЕсли;
	МассивПараметров = Новый Массив;
	Для Каждого Параметр Из Параметры Цикл
		МассивПараметров.Добавить(Новый Структура("key, value", Параметр.Ключ, Параметр.Значение));
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ЗапросВыполненУспешно", Ложь);
	Результат.Вставить("Ответ", "");
	
	Соединение = GitLabСоединение(Проект.РезультатыВыполненияТестовАдресСервера,180);
	КодСостояния = 200;
	Попытка
		ДанныеЗапроса = Новый Структура;
		ДанныеЗапроса.Вставить("ref", ИмяВетки);
		ДанныеЗапроса.Вставить("variables", МассивПараметров);
		Ресурс = СтрШаблон("/api/v4/projects/%1/pipeline", 
								Проект.РезультатыВыполненияТестовИмяПроектаНаСервере);
		Ответ = GitLabЗапрос(Соединение, Ресурс, Проект.РезультатыВыполненияТестовТокен,"POST",ДанныеЗапроса,КодСостояния);
		Результат.ЗапросВыполненУспешно = Истина;
		Результат.Ответ  = XMLСтрока(Ответ.id);
		
	Исключение
		Результат.ЗапросВыполненУспешно = Ложь;
		ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Если КодСостояния = 400 И СтрНайти(ТекстОшибки, "Reference not found") <> 0 Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						  НСтр("ru = 'Ветка ""%1"" не найдена на сервере.'"),
						  ИмяВетки);
		КонецЕсли;
		
		Результат.Ответ = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
							НСтр("ru = 'Не удалось запустить тестирование. Возникла ошибка: %1'"),
							ТекстОшибки);
	КонецПопытки;
	Соединение = Неопределено;
	Возврат Результат;

КонецФункции

//Отменяет созданный запуск тестирования на сервере.
//
// Параметры:
//  Проект                - СправочникСсылка.Проекты - Проект, для которого отменяется запуск.
//  ИдентификаторЗапуска  - Строка - Идентификатор запуска на сервере. 
// 
// Возвращаемое значение:
//  Структура - 
//     * ЗапросВыполненУспешно  - Булево - Истина, если запуск успешно был отменен и Ложь если нет;
//     * Ответ                  - Строка - Текст исключения;
//  
Функция ОтменитьТестирование(Проект, ИдентификаторЗапуска) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ЗапросВыполненУспешно", Ложь);
	Результат.Вставить("Ответ", "");
	
	Соединение = GitLabСоединение(Проект.РезультатыВыполненияТестовАдресСервера,180);
	КодСостояния = 200;
	Попытка
		Ресурс = СтрШаблон("/api/v4/projects/%1/pipelines/%2/cancel", 
							Проект.РезультатыВыполненияТестовИмяПроектаНаСервере, ИдентификаторЗапуска);
		Ответ = GitLabЗапрос(Соединение, Ресурс, Проект.РезультатыВыполненияТестовТокен,"POST",,КодСостояния);
		Результат.ЗапросВыполненУспешно = Истина;
		Результат.Ответ  = XMLСтрока(Ответ.id);	
	Исключение
		Результат.ЗапросВыполненУспешно = Ложь;
		ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Если КодСостояния = 404 ИЛИ КодСостояния = 400 Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						  НСтр("ru = 'Запуск %1 не существует на сервере.'"),
						  ИдентификаторЗапуска);
		КонецЕсли;
			
		Результат.Ответ = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
							НСтр("ru = 'Не удалось запустить тестирование. %1'"),
							ТекстОшибки);
	КонецПопытки;
	Соединение = Неопределено;
	Возврат Результат;
	
КонецФункции

//Информация о ветке, полученная у сервера тестирования.
//
// Параметры:
//  Проект   - СправочникСсылка.Проекты - Проект, для которого считывается информация.
//  ИмяВетки - Строка                   - Имя ветки. 
// 
// Возвращаемое значение:
//  Структура - данные о ветке, полученные у сервера тестирования.
//  
Функция ИнформацияОВетке(Проект, ИмяВетки) Экспорт
	
	Соединение = GitLabСоединение(Проект.РезультатыВыполненияТестовАдресСервера);
	Ресурс = СтрШаблон("/api/v4/projects/%1/repository/branches/%2", 
						Проект.РезультатыВыполненияТестовИмяПроектаНаСервере, 
						СтрЗаменить(ИмяВетки, "/", "%2F"));
	Ответ = GitLabЗапрос(Соединение, Ресурс, Проект.РезультатыВыполненияТестовТокен);
	Соединение = Неопределено;
	Возврат Ответ;
	
КонецФункции

//Информация о коммите, полученная у сервера тестирования.
//
// Параметры:
//  Проект   - СправочникСсылка.Проекты - Проект, для которого считывается информация.
//  Коммит - Строка                   - Хеш коммита.
// 
// Возвращаемое значение:
//  Структура - данные о коммите, полученные у сервера тестирования.
//  
Функция ИнформацияОКоммите(Проект, Коммит) Экспорт
	
	Соединение = GitLabСоединение(Проект.РезультатыВыполненияТестовАдресСервера);
	Ресурс = СтрШаблон("/api/v4/projects/%1/repository/commits/%2", 
						Проект.РезультатыВыполненияТестовИмяПроектаНаСервере, 
						Коммит);
	Ответ = GitLabЗапрос(Соединение, Ресурс, Проект.РезультатыВыполненияТестовТокен);
	Соединение = Неопределено;
	Возврат Ответ;
	
КонецФункции

//Информация о запуске тестирования, полученная у сервера тестирования.
//
// Параметры:
//  Проект   - СправочникСсылка.Проекты - Проект, для которого считывается информация.
//  ИдентификаторЗапуска  - Строка - Идентификатор запуска на сервере. 
// 
// Возвращаемое значение:
//  Структура - данные о запуске тестирования, полученные у сервера тестирования.
//  
Функция ИнформацияОЗапуске(Проект, ИдентификаторЗапуска) Экспорт
	ИдентификаторЗапускаXML = XMLСтрока(ИдентификаторЗапуска);

	Соединение = GitLabСоединение(Проект.РезультатыВыполненияТестовАдресСервера);
	Ресурс = СтрШаблон("/api/v4/projects/%1/pipelines/%2", 
						Проект.РезультатыВыполненияТестовИмяПроектаНаСервере, 
						ИдентификаторЗапускаXML);
	Ответ = GitLabЗапрос(Соединение, Ресурс, Проект.РезультатыВыполненияТестовТокен);
	Соединение = Неопределено;
	Возврат Ответ;
	
КонецФункции

//Информация о мердж реквестах, созданных от ветки, полученная у сервера тестирования.
//
// Параметры:
//  Проект            - СправочникСсылка.Проекты - Проект, для которого считывается информация.
//  ИмяВеткиИсточника - Строка                   - Имя ветки источника мердж реквеста.
//  ИмяВеткиПриемника - Строка                   - Имя ветки приемника мердж реквеста. 
// 
// Возвращаемое значение:
//  Массив - данные о мердж реквестах, полученные у сервера тестирования.
// 
Функция CписокМерджРеквестов(Проект, ИмяВеткиИсточника, ИмяВеткиПриемника=Неопределено, Статус=Неопределено) Экспорт
	
	Соединение = GitLabСоединение(Проект.РезультатыВыполненияТестовАдресСервера);
	Ресурс = СтрШаблон("/api/v4/projects/%1/merge_requests?source_branch=%2&order_by=updated_at", 
						Проект.РезультатыВыполненияТестовИмяПроектаНаСервере, 
						СтрЗаменить(ИмяВеткиИсточника, "/", "%2F"));
	Если Статус <> Неопределено Тогда 
		Ресурс = Ресурс + "&state=" + Статус;
	КонецЕсли;
	Если ИмяВеткиПриемника <> Неопределено Тогда 
		Ресурс = Ресурс + "&target_branch=" + СтрЗаменить(ИмяВеткиПриемника, "/", "%2F");
	КонецЕсли;
	Ответ = GitLabЗапрос(Соединение, Ресурс, Проект.РезультатыВыполненияТестовТокен);
	Соединение = Неопределено;
	Возврат Ответ;
	
КонецФункции

//Информация о коммитах в мердж реквесте.
//
// Параметры:
//  Проект                     - СправочникСсылка.Проекты - Проект, для которого считывается информация.
//  ИдентификаторМерджРеквеста - Строка                   - Имя ветки источника мердж реквеста.
// 
// Возвращаемое значение:
//  Массив - данные о коммитах в мердж реквесте.
// 
Функция CписокКоммитовМерджРеквеста(Проект, ИдентификаторМерджРеквеста) Экспорт
	ИдентификаторМерджРеквеста = XMLСтрока(ИдентификаторМерджРеквеста);

	Соединение = GitLabСоединение(Проект.РезультатыВыполненияТестовАдресСервера);
	Ресурс = СтрШаблон("/api/v4/projects/%1/merge_requests/%2/commits", 
						Проект.РезультатыВыполненияТестовИмяПроектаНаСервере, 
						ИдентификаторМерджРеквеста);
	Ответ = GitLabЗапрос(Соединение, Ресурс, Проект.РезультатыВыполненияТестовТокен,,,,Истина);
	Соединение = Неопределено;
	Возврат Ответ;
	
КонецФункции

//Информация о различиях между двумя ветками, полученная у сервера тестирования.
//
// Параметры:
//  Проект      - СправочникСсылка.Проекты - Проект, для которого считывается информация.
//  ПерваяВетка - Строка                   - Имя ветки или хеш коммита для сравнения. 
//  ВтораяВетка - Строка                   - Имя ветки или хеш коммита для сравнения. 
// 
// Возвращаемое значение:
//  Структура - данные о различиях между ветками, полученные у сервера тестирования.
// 
Функция РазницаМеждуВетками(Проект, ПерваяВетка, ВтораяВетка) Экспорт
	РеквизитыПроекта = 
		ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Проект, "РезультатыВыполненияТестовАдресСервера, 
															|РезультатыВыполненияТестовИмяПроектаНаСервере, 
															|РезультатыВыполненияТестовТокен");
	Соединение = GitLabСоединение(РеквизитыПроекта.РезультатыВыполненияТестовАдресСервера);
	Ресурс = СтрШаблон("/api/v4/projects/%1/repository/compare?from=%2&to=%3", 
							РеквизитыПроекта.РезультатыВыполненияТестовИмяПроектаНаСервере, 
							СтрЗаменить(ПерваяВетка, "/", "%2F"), СтрЗаменить(ВтораяВетка, "/", "%2F"));
	Ответ = GitLabЗапрос(Соединение, Ресурс, РеквизитыПроекта.РезультатыВыполненияТестовТокен);
	Соединение = Неопределено;
	Возврат Ответ;
	
КонецФункции

// Формирует гиперссылку на коммит в системе хранения кода по хешу коммита.
//
// Параметры:
//  Проект        - Тип - СправочникСсылка.Проекты.
//  Идентификатор - Тип - Строка.
//
// Возвращаемое значение:
//  Гиперссылка - Тип - Строка.
//
Функция АдресКоммита(Проект, Идентификатор) Экспорт
	
	Возврат СсылкаПоПроекту(Проект) + "-/commit/" + Идентификатор;
	
КонецФункции 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
	
Процедура ЗапуститьОтложенныеЗапускиТестирования(Проект)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗапускТестирования.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗапускТестирования КАК ЗапускТестирования
	|ГДЕ
	|	ЗапускТестирования.Проект = &Проект
	|	И НЕ ЗапускТестирования.Проведен
	|	И НЕ ЗапускТестирования.ПометкаУдаления
	|	И ПОДСТРОКА(ЗапускТестирования.Pipeline_ID, 1, 1) ПОДОБНО """"
	|	И ЗапускТестирования.Настройка <> ЗНАЧЕНИЕ(Справочник.НастройкиЗапускаТестирования.ПустаяСсылка)
	|	И ЗапускТестирования.Дата МЕЖДУ &ДеньНазад И &ТекущаяДата";
	
	ТекущаяДата = ТекущаяДата();
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата);
	Запрос.УстановитьПараметр("ДеньНазад", ТекущаяДата-24*60*60);
	Запрос.УстановитьПараметр("Проект", Проект);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Попытка
			ЗапускОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ЗапускОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Запуск тестирования.Проведение отложенного запуска тестирования'"),
										УровеньЖурналаРегистрации.Ошибка,,,ОписаниеОшибки());
		КонецПопытки;
	КонецЦикла;

КонецПроцедуры

Функция ПроектыКОбработке()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Проекты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Проекты КАК Проекты
		|ГДЕ
		|	Проекты.ЗапускатьАвтоматическоеТестирование";
	
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции	 

Функция GitLabСоединение(Сервер,Таймаут=360)
	
	OpenSSL = Новый ЗащищенноеСоединениеOpenSSL;
	Соединение = Новый HTTPСоединение(Сервер,,,,, Таймаут, OpenSSL);
	
	Возврат Соединение;
	
КонецФункции

Функция GitLabЗапрос(Соединение, Ресурс, Токен, Метод="GET", Данные=Неопределено, 
					КодСостояния=Неопределено, ЦиклПоСтраницам=Ложь, МаксимумЗаписейПоСтраницам=500)
	СчетчикСтраниц = 1;
	КоличествоЗаписейВСтранице = 100;
	Результат = Новый Массив;
	Пока Истина Цикл
		Запрос = Новый HTTPЗапрос;
		Запрос.АдресРесурса = Ресурс;
		Если ЦиклПоСтраницам Тогда
			ПервыйСимвол = "?";
			Если СтрНайти(Запрос.АдресРесурса, "?") > 0 Тогда
				ПервыйСимвол = "&";
			КонецЕсли;
			Запрос.АдресРесурса = Запрос.АдресРесурса + ПервыйСимвол + СтрШаблон("page=%1&per_page=%2", СчетчикСтраниц, КоличествоЗаписейВСтранице);
		КонецЕсли;
		Запрос.Заголовки.Вставить("PRIVATE-TOKEN", Токен);
		Если Данные<> Неопределено Тогда
			ЗаписьJSON = Новый ЗаписьJSON;
			ЗаписьJSON.УстановитьСтроку();
			ЗаписатьJSON(ЗаписьJSON, Данные);
			
			Запрос.Заголовки.Вставить("Content-Type", "application/json; charset=UTF-8");
			Запрос.УстановитьТелоИзСтроки(ЗаписьJSON.Закрыть());
			ЗаписьJSON = Неопределено;
		КонецЕсли;
		
		Ответ = Соединение.ВызватьHTTPМетод(Метод, Запрос);
		КодСостояния = Ответ.КодСостояния;
		СтрокаРезультата = Ответ.ПолучитьТелоКакСтроку();

		Запрос = Неопределено;
		Если Цел(Ответ.КодСостояния/100) <> 2 Тогда 
			ТекстИсключения = СтрокаРезультата;
			Попытка
				Результат = СтрокаJSONВСтруктуру(СтрокаРезультата);
				Если Результат.Свойство("error") Тогда
					ТекстИсключения = Результат.error;
				ИначеЕсли Результат.Свойство("message") Тогда 
					ТекстИсключения = Результат.message;
					Если ТипЗнч(ТекстИсключения) = Тип("Структура") 
						И ТекстИсключения.Свойство("base") Тогда
						ТекстИсключения = ТекстИсключения.base;
						Если ТипЗнч(ТекстИсключения) = Тип("Массив") Тогда
							ТекстИсключения = СтрСоединить(ТекстИсключения, Символы.ПС); 
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			Исключение
				ТекстИсключения = СтрокаРезультата;
			КонецПопытки;
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Запуск тестирования.Gitlab запрос'"),
								УровеньЖурналаРегистрации.Ошибка,,,
								СтрШаблон(
									НСтр("ru = 'При запросе: %1
									           |Сервер вернул код состояния: %2
											   |Строка результата: %3'"),
									Ресурс,
									Ответ.КодСостояния,
									СтрокаРезультата));
			
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
		ТекущийОтвет = СтрокаJSONВСтруктуру(СтрокаРезультата);
		Если НЕ ЦиклПоСтраницам Тогда
			Результат = ТекущийОтвет;
			Прервать;
		КонецЕсли;
		
		Если ТекущийОтвет.Количество() = 0 Тогда
			Прервать;
		КонецЕсли;
		Для Каждого Элемент Из ТекущийОтвет Цикл
			Результат.Добавить(Элемент);
		КонецЦикла;
		Если СчетчикСтраниц * КоличествоЗаписейВСтранице > МаксимумЗаписейПоСтраницам Тогда
			Прервать;
		КонецЕсли;
		
		СчетчикСтраниц = СчетчикСтраниц+ 1;
	
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

Функция СтрокаJSONВСтруктуру(Значение, ИменаСвойствСоЗначениямиДата = Неопределено)
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(Значение);
	
	Если ИменаСвойствСоЗначениямиДата = Неопределено Тогда
		ИменаСвойствСоЗначениямиДата = Новый Массив;
	КонецЕсли;	 
	ИменаСвойствСоЗначениямиДата.Добавить("created_at");
	ИменаСвойствСоЗначениямиДата.Добавить("ДатаПоследнейПроверки");
	
	Результат = ПрочитатьJSON(ЧтениеJSON,, ИменаСвойствСоЗначениямиДата);
	
	ИменаСвойствДатаНеопределено = Новый Массив;
	ИменаСвойствДатаНеопределено.Добавить("updated_at");
	ИменаСвойствДатаНеопределено.Добавить("finished_at");
	ИменаСвойствДатаНеопределено.Добавить("started_at");
	ИменаСвойствДатаНеопределено.Добавить("artifacts_expire_at");
	ИменаСвойствДатаНеопределено.Добавить("merged_at");
	
	РезультатМассив = Новый Массив;
	Если Тип("Структура") = ТипЗнч(Результат) Тогда
		РезультатМассив.Добавить(Результат);
	Иначе 
		РезультатМассив = Результат;
	КонецЕсли;
	Для Каждого ЭлементРезультата Из РезультатМассив Цикл
		Если НЕ Тип("Структура") = ТипЗнч(ЭлементРезультата) Тогда
			Продолжить;
		КонецЕсли;
		Для Каждого Имя Из ИменаСвойствДатаНеопределено Цикл
			Если ЭлементРезультата.Свойство(Имя) 
				И ЭлементРезультата[Имя] <> Неопределено Тогда
				ЭлементРезультата[Имя] = ПрочитатьДатуJSON(ЭлементРезультата[Имя], ФорматДатыJSON.ISO);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ИменаСвойствСоЗначениямиДата = Неопределено;
	ИменаСвойствДатаНеопределено = Неопределено;
	
	ЧтениеJSON.Закрыть();
	
	Возврат Результат;
	
КонецФункции

//Возвращает время начала тестирования проекта по дате.
//
// Параметры:
//  Проект                - СправочникСсылка.Проекты  - Проект, для которого формируется расписание.
//  ТекущаяДата           - Дата                      - Дата, на которую формируется расписание. 
//
// Возвращаемое значение:
//   ДатаВремя - Время начала тестирования проекта.
Функция ВремяНачалаТестированияПоДате(Проект, ТекущаяДата=Неопределено)
	
	Если ТекущаяДата = Неопределено Тогда
		ТекущаяДата = ТекущаяДата();
	КонецЕсли;

	ДатаНачала = НачалоДня(ТекущаяДата);
	СдвигИндекса = (ДеньНедели(ДатаНачала)-1)*24;
	
	Количество = Проект.СлотыТестирования.Количество();
	Для Час = 0 По 12 Цикл
		Индекс = СдвигИндекса + Час;
		Если Индекс >= Количество ИЛИ Индекс < 0 Тогда
			Прервать;
		КонецЕсли;
		Если Проект.СлотыТестирования[Индекс].ДоступнаяМощность = 0 Тогда
			ДатаНачала = НачалоДня(ДатаНачала) + Час*60*60;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДатаНачала;
	
КонецФункции

Функция ВремяОкончанияТестированияПоДате(Проект, ДатаНачала)
	
	СпустяСутки = ДатаНачала+24*60*60;
	ДатаОкончания = Макс(СпустяСутки, ВремяНачалаТестированияПоДате(Проект, СпустяСутки));
	Возврат ДатаОкончания;
	
КонецФункции

Процедура ДобавитьСообщениеПоОбъекту(СсылкаНаОбъект, ПользовательПолучательСообщения, ПользовательАвторСообщения, ДатаНачала, 
									ТекстСообщения, ШаблонСообщения = Неопределено)
	Попытка
	
		СистемаВзаимодействияДоступна = СистемаВзаимодействия.ИспользованиеДоступно();
		Если НЕ (СистемаВзаимодействияДоступна И ПользовательПолучательСообщения <> ПользовательАвторСообщения) Тогда
			Возврат;
		КонецЕсли;
		
		ЗаголовокОбсуждения = НСтр("ru='Тестирование ветки'"); 
		Если ШаблонСообщения <> Неопределено Тогда
			ИмеющиесяСообщения = СобщенияПоОбъектуБезОтбораПоЗаголовку(
												 СсылкаНаОбъект,
												 ПользовательПолучательСообщения,
												 ПользовательАвторСообщения,
												 ДатаНачала,
												 ШаблонСообщения);
				
			Если ИмеющиесяСообщения.Количество()>0 Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
							
		РаботаССистемойВзаимодействия.ДобавитьСообщениеПоОбъекту(СсылкаНаОбъект, 
													 ПользовательПолучательСообщения,
													 ПользовательАвторСообщения,
													 ЗаголовокОбсуждения,
													 ТекстСообщения);
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Запуск тестирования. Ошибка при работе с системой взаимодействия.'"),
								УровеньЖурналаРегистрации.Ошибка,,
								Неопределено,
								ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	
	КонецПопытки;

КонецПроцедуры

Функция СобщенияПоОбъектуБезОтбораПоЗаголовку(СсылкаНаОбъект, ПользовательПолучательСообщения, ПользовательАвторСообщения, ДатаНачала, ТекстСообщения = Неопределено)

	ИдентификаторАдресатаСообщения = РаботаССистемойВзаимодействия.ПользовательДляСистемыВзаимодействия(ПользовательПолучательСообщения);
	ИдентификаторАвтораСообщения = РаботаССистемойВзаимодействия.ПользовательДляСистемыВзаимодействия(ПользовательАвторСообщения);
	
	НавигационнаяСсылкаПредмета = ПолучитьНавигационнуюСсылку(СсылкаНаОбъект);
	КонтекстОбсуждения = Новый КонтекстОбсужденияСистемыВзаимодействия(НавигационнаяСсылкаПредмета);
	ОтборОбсуждений = Новый ОтборОбсужденийСистемыВзаимодействия;
	
	ОтборОбсуждений.Групповое = Истина;
	ОтборОбсуждений.КонтекстноеОбсуждение = Истина;
	ОтборОбсуждений.КонтекстОбсуждения = КонтекстОбсуждения;
	ОтборОбсуждений.Отображаемое = Истина;
	ОтборОбсуждений.НаправлениеСортировки = НаправлениеСортировки.Возр;
	ОтборОбсуждений.ДатаНачала = ДатаНачала;
	ОтборОбсуждений.ТекущийПользовательЯвляетсяУчастником = Ложь;
	
	НайденныеОбсуждения = СистемаВзаимодействия.ПолучитьОбсуждения(ОтборОбсуждений);
	НайденныеСообщения = Новый Массив;
	Для Каждого Обсуждение из НайденныеОбсуждения Цикл
		ОтборСообщений = Новый ОтборСообщенийСистемыВзаимодействия;
		ОтборСообщений.Обсуждение = Обсуждение.Идентификатор;
		
		Сообщения = СистемаВзаимодействия.ПолучитьСообщения(ОтборСообщений);
		
		Для Каждого Сообщение из Сообщения Цикл
			Если Сообщение.Дата >= ДатаНачала 
				И Сообщение.Автор = ИдентификаторАвтораСообщения
				И Сообщение.Получатели.Содержит(ИдентификаторАдресатаСообщения)
				И (ТекстСообщения = Неопределено ИЛИ СтрНайти(Сообщение.Текст, ТекстСообщения) > 0) Тогда
				НайденныеСообщения.Добавить(Сообщение);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	Возврат НайденныеСообщения;
КонецФункции

Функция СсылкаПоПроекту(Проект)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Проекты.АдресGitСервера КАК АдресGitСервера
		|ИЗ
		|	Справочник.Проекты КАК Проекты
		|ГДЕ
		|	Проекты.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Проект);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстСсылки = "";
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ТекстСсылки = ВыборкаДетальныеЗаписи.АдресGitСервера;
	КонецЦикла;
	
	ТекстСсылки = СтрЗаменить(ТекстСсылки,"git@","");
	ТекстСсылки = СтрЗаменить(ТекстСсылки,":","/");
	ТекстСсылки = СтрЗаменить(ТекстСсылки,".git","");
	
	Если Прав(ТекстСсылки,1) <> "/" Тогда
		ТекстСсылки = ТекстСсылки + "/";
	КонецЕсли;	 
	
	ТекстСсылки = "https://" + ТекстСсылки;
	Возврат ТекстСсылки;
	
КонецФункции

Функция ЕстьРазличияВКодеКонфигурации(ДанныеПоРазличиям, ПутьКПроектуВРепозитории)
	
	ПутьКИсходникам = ПутьКПроектуВРепозитории + "/src/";
	ПутиИсключения = ПутиИсключения();
	
	Для Каждого ТекИзменение Из ДанныеПоРазличиям Цикл
		ПутьКФайлуНРег = НРег(ТекИзменение.new_path);
		
		ИзмененныйФайл = Новый Файл(ПутьКФайлуНРег);
		ФайлРодительскогоКаталога = Новый Файл(ИзмененныйФайл.Путь);
		Если НРег(ФайлРодительскогоКаталога.Имя) = "help" Тогда
			// измененные файлы справки игнорируются
			Продолжить;
		КонецЕсли;	 
		
		Если Лев(ПутьКФайлуНРег, СтрДлина(ПутьКИсходникам)) = НРег(ПутьКИсходникам) Тогда
			ЭтоИсключение = Ложь;
			Для Каждого ТекИсключение Из ПутиИсключения Цикл
				Если Прав(ПутьКФайлуНРег, СтрДлина(ТекИсключение)) = НРег(ТекИсключение) Тогда
					ЭтоИсключение = Истина;
					Прервать;
				КонецЕсли;	 
			КонецЦикла;	 
			
			Если НЕ ЭтоИсключение Тогда
				// значит это изменения в коде конфигурации
				Возврат Истина;
			КонецЕсли;	 
			
		КонецЕсли;	 
		
	КонецЦикла;	 
	
	Возврат Ложь;
	
КонецФункции	 

Функция ПутиИсключения()
	
	Массив = Новый Массив;
	Массив.Добавить("/src/Configuration/MobileClientSign.bin");
	
	Возврат Массив;
	
КонецФункции	 

#КонецОбласти
