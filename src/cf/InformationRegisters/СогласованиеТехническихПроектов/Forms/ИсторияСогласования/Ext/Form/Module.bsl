#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ТехническийПроект,"Владелец");
	
	ДоступнаОтменаЛюбыхСогласований	=
		УправлениеДоступомСППР.РольДоступнаПоПроекту("ПланированиеИУправлениеТехническимиПроектами", Проект);
		
	ДоступнаОтменаСвоихСогласований =
		УправлениеДоступомСППР.РольДоступнаПоПроекту("ИзменениеСогласованияТехническихПроектов", Проект);
		
	Если Элементы.Найти("ФормаОтменитьСогласование") <> Неопределено Тогда
		Элементы.ФормаОтменитьСогласование.Видимость = ДоступнаОтменаЛюбыхСогласований ИЛИ ДоступнаОтменаСвоихСогласований;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ОтборСогласованийПоПредстоящейКТ = Настройки.Получить("ТолькоПредстоящаяКонтрольнаяТочка");
	ОтборТолькоМоихСогласований = Настройки.Получить("ТолькоЯСогласующий");
	
	Если ОтборСогласованийПоПредстоящейКТ = Неопределено Тогда
		ОтборСогласованийПоПредстоящейКТ = Ложь;
	КонецЕсли;
	
	Если ОтборТолькоМоихСогласований = Неопределено Тогда
		ОтборТолькоМоихСогласований = Ложь;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
																			"Согласующий",
																			ТекущийПользователь,
																			ВидСравненияКомпоновкиДанных.Равно,
																			,																			
																			ОтборТолькоМоихСогласований);
																			
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
																			"СогласованиеПредстоящейКонтрольнойТочки",
																			Истина,
																			ВидСравненияКомпоновкиДанных.Равно,
																			,
																			ОтборСогласованийПоПредстоящейКТ);
																			
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТолькоПредстоящаяКонтрольнаяТочкаПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
																			"СогласованиеПредстоящейКонтрольнойТочки",
																			Истина,
																			ВидСравненияКомпоновкиДанных.Равно,
																			,																			
																			ТолькоПредстоящаяКонтрольнаяТочка);
КонецПроцедуры

&НаКлиенте
Процедура ТолькоЯСогласующийПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
																			"Согласующий",
																			ТекущийПользователь,
																			ВидСравненияКомпоновкиДанных.Равно,
																			,																			
																			ТолькоЯСогласующий);
																			
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтменитьСогласование(Команда)
	
	ОчиститьСообщения();
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		ТекстСообщения = НСтр("ru='Не указана строка согласования для отмены'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если Элементы.Список.ВыделенныеСтроки.Количество()>1 Тогда
		ТекстСообщения = НСтр("ru='Для отмены согласования необходимо указать только одну строку'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если НЕ ДоступнаОтменаЛюбыхСогласований И ТекущиеДанные.Установил <> ТекущийПользователь Тогда
		ТекстСообщения = НСтр("ru='Возможна отмена только собственных согласований'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ОтменитьСогласованиеСервер(ТекущиеДанные);
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ТехническийПроект", ТекущиеДанные.ТехническийПроект);
	СтруктураПараметров.Вставить("ДатаСогласования", ТекущиеДанные.Период);
	СтруктураПараметров.Вставить("КлючСтрокиКонтрольнойТочки", ТекущиеДанные.КлючСтрокиКонтрольнойТочки);
	СтруктураПараметров.Вставить("Согласующий", ТекущиеДанные.Согласующий);
	СтруктураПараметров.Вставить("РольСогласующего", ТекущиеДанные.РольСогласующего);
	
	Оповестить("ОтменаСогласованияКонтрольнойТочкиТехническогоПроекта", СтруктураПараметров);
	
КонецПроцедуры

&НаСервере
Процедура ОтменитьСогласованиеСервер(ДанныеСтроки)
	
	МенеджерЗаписи = РегистрыСведений.СогласованиеТехническихПроектов.СоздатьМенеджерЗаписи();
	
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ДанныеСтроки);
	
	Попытка
		МенеджерЗаписи.Удалить();
	Исключение
		ВызватьИсключение(ОписаниеОшибки());
	КонецПопытки;
	
	ЗаписатьТехническийПроект(ДанныеСтроки.ТехническийПроект, ДанныеСтроки.КлючСтрокиКонтрольнойТочки);
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьТехническийПроект(ТехническийПроект, КлючСтрокиКонтрольнойТочки)
	
	ТехническийПроектОбъект = ТехническийПроект.ПолучитьОбъект();
	
	Попытка
		ТехническийПроектОбъект.Заблокировать();
	Исключение
		Возврат;
	КонецПопытки;
	
	Попытка
		
		МассивКлючейСтрок = Новый Массив;
		МассивКлючейСтрок.Добавить(КлючСтрокиКонтрольнойТочки);
		
		ТехническийПроектОбъект.ДополнительныеСвойства.Вставить("ВыполнятьОткатКонтрольнойТочки", Истина);
		ТехническийПроектОбъект.ДополнительныеСвойства.Вставить("МассивКлючейНесогласованныхТочек", МассивКлючейСтрок);
			
		ТехническийПроектОбъект.Записать();
		ТехническийПроектОбъект.Разблокировать();
	Исключение
	КонецПопытки;

КонецПроцедуры

#КонецОбласти