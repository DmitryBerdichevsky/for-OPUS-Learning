#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Заполняет список текущих дел пользователя.
//
// Параметры:
//  ТекущиеДела - ТаблицаЗначений - таблица значений с колонками:
//    * Идентификатор - Строка - внутренний идентификатор дела, используемый механизмом "Текущие дела".
//    * ЕстьДела      - Булево - если Истина, дело выводится в списке текущих дел пользователя.
//    * Важное        - Булево - если Истина, дело будет выделено красным цветом.
//    * Представление - Строка - представление дела, выводимое пользователю.
//    * Количество    - Число  - количественный показатель дела, выводится в строке заголовка дела.
//    * Форма         - Строка - полный путь к форме, которую необходимо открыть при нажатии на гиперссылку
//                               дела на панели "Текущие дела".
//    * ПараметрыФормы- Структура - параметры, с которыми нужно открывать форму показателя.
//    * Владелец      - Строка, объект метаданных - строковый идентификатор дела, которое будет владельцем для текущего
//                      или объект метаданных подсистема.
//    * Подсказка     - Строка - текст подсказки.
//
Процедура ПриЗаполненииСпискаТекущихДел(ТекущиеДела) Экспорт
	
	Если Не ПравоДоступа("Редактирование", Метаданные.РегистрыСведений.СогласованиеТехническихПроектов) Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторГруппыТехническиеПроекты = НСтр("ru='Технические проекты'");
	
	ПоказателиТекущихДел = ПоказателиТекущихДелПоСогласованиюТехническихПроектов();
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	СписокСроков = Новый СписокЗначений;
	СписокСроков.Добавить("Вчера");
	СписокСроков.Добавить("Сегодня");
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Действие", Новый СписокЗначений);
	ПараметрыОтбора.Вставить("СрокОтработки", СписокСроков);
	
	ПараметрыОтбораСогласовать = Новый Структура;
	ПараметрыОтбораПодготовитьКСогласованию	= Новый Структура;
	
	Для Каждого ЭлементСтруктуры из ПараметрыОтбора Цикл
		ПараметрыОтбораСогласовать.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
		ПараметрыОтбораПодготовитьКСогласованию.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
	КонецЦикла;
	
	СписокДействийСогласовать = Новый СписокЗначений;
	СписокДействийСогласовать.Добавить("Согласовать");
	
	СписокДействийПодготовитьКСогласованию = Новый СписокЗначений;
	СписокДействийПодготовитьКСогласованию.Добавить("Подготовить к согласованию");
	
	ПараметрыОтбораСогласовать.Вставить("Действие", СписокДействийСогласовать);
	ПараметрыОтбораПодготовитьКСогласованию.Вставить("Действие", СписокДействийПодготовитьКСогласованию);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлюЧНазначенияИспользования", "ТекущиеДела");
	ПараметрыФормы.Вставить("СтруктураБыстрогоОтбора", ПараметрыОтбораПодготовитьКСогласованию);
	
	Дело = ТекущиеДела.Добавить();
	Дело.Идентификатор  = "ТехническиеПроектыПодготовитьКСогласованию";
	Дело.ЕстьДела       = ПоказателиТекущихДел.КоличествоПодготовитьКСогласованиюТехническихПроектов > 0;
	Дело.Представление  = НСтр("ru = 'Подготовить к согласованию'");
	Дело.Количество     = ПоказателиТекущихДел.КоличествоПодготовитьКСогласованиюТехническихПроектов;
	Дело.Важное 		= Ложь;
	Дело.ПараметрыФормы = ПараметрыФормы;
	Дело.Форма          = "РегистрСведений.СогласованиеТехническихПроектов.Форма.СогласованиеТехническихПроектов";
	Дело.Владелец       = ИдентификаторГруппыТехническиеПроекты;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлюЧНазначенияИспользования", "ТекущиеДела");
	ПараметрыФормы.Вставить("СтруктураБыстрогоОтбора", ПараметрыОтбораСогласовать);
	
	Дело = ТекущиеДела.Добавить();
	Дело.Идентификатор  = "ТехническиеПроектыСогласовать";
	Дело.ЕстьДела       = ПоказателиТекущихДел.КоличествоСогласованияТехническихПроектов > 0;
	Дело.Представление  = НСтр("ru = 'Согласовать'");
	Дело.Количество     = ПоказателиТекущихДел.КоличествоСогласованияТехническихПроектов;
	Дело.Важное 		= Ложь;
	Дело.ПараметрыФормы = ПараметрыФормы;
	Дело.Форма          = "РегистрСведений.СогласованиеТехническихПроектов.Форма.СогласованиеТехническихПроектов";
	Дело.Владелец       = ИдентификаторГруппыТехническиеПроекты;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ТехническийПроект.Владелец)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПоказателиТекущихДелПоСогласованиюТехническихПроектов()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА СогласованиеТехническихПроектов.СрокОтработкиПоручения < &ДатаЗавтра
	|					И ТехническиеПроектыКонтрольныеТочки.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыКонтрольныхТочекТехническихПроектов.Пропущена)
	|					И СогласованиеТехническихПроектов.ТехническийПроект.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыТехническихПроектов.НеЗапланирован), ЗНАЧЕНИЕ(Перечисление.СтатусыТехническихПроектов.Запланирован), ЗНАЧЕНИЕ(Перечисление.СтатусыТехническихПроектов.Активен))
	|					И СогласованиеТехническихПроектов.СтатусСогласования = ЗНАЧЕНИЕ(Перечисление.СтатусыСогласованияТехническихПроектов.ГотовоКСогласованию)
	|					И СогласованиеТехническихПроектов.Согласующий = &ТекущийПользователь
	|				ТОГДА СогласованиеТехническихПроектов.ТехническийПроект
	|		КОНЕЦ) КАК КоличествоСогласованияТехническихПроектов,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА СогласованиеТехническихПроектов.Период < &ДатаЗавтра
	|						И ТехническиеПроектыКонтрольныеТочки.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыКонтрольныхТочекТехническихПроектов.Пропущена)
	|						И СогласованиеТехническихПроектов.ТехническийПроект.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыТехническихПроектов.НеЗапланирован), ЗНАЧЕНИЕ(Перечисление.СтатусыТехническихПроектов.Запланирован), ЗНАЧЕНИЕ(Перечисление.СтатусыТехническихПроектов.Активен))
	|						И СогласованиеТехническихПроектов.СтатусСогласования = ЗНАЧЕНИЕ(Перечисление.СтатусыСогласованияТехническихПроектов.НеСогласовано)
	|						И СогласованиеТехническихПроектов.ТехническийПроект.Ответственный = &ТекущийПользователь
	|					ИЛИ ТехническиеПроектыКонтрольныеТочки.СрокПрохождения < &ДатаЗавтра
	|						И ТехническиеПроектыКонтрольныеТочки.КонтрольнаяТочка = ТехническиеПроектыКонтрольныеТочки.Ссылка.ПредстоящаяКонтрольнаяТочка
	|						И ТехническиеПроектыКонтрольныеТочки.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыКонтрольныхТочекТехническихПроектов.Назначена)
	|						И СогласованиеТехническихПроектов.СтатусСогласования = ЗНАЧЕНИЕ(Перечисление.СтатусыСогласованияТехническихПроектов.НеГотовоКСогласованию)
	|						И СогласованиеТехническихПроектов.ТехническийПроект.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыТехническихПроектов.Активен)
	|						И СогласованиеТехническихПроектов.ТехническийПроект.Ответственный = &ТекущийПользователь
	|				ТОГДА СогласованиеТехническихПроектов.ТехническийПроект
	|		КОНЕЦ) КАК КоличествоПодготовитьКСогласованиюТехническихПроектов
	|ИЗ
	|	РегистрСведений.СогласованиеТехническихПроектов.СрезПоследних(
	|			,
	|			(Согласующий = &ТекущийПользователь
	|				ИЛИ ТехническийПроект.Ответственный = &ТекущийПользователь)
	|				И ТехническийПроект.ИспользуютсяКонтрольныеТочкиПроекта) КАК СогласованиеТехническихПроектов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТехническиеПроекты.КонтрольныеТочки КАК ТехническиеПроектыКонтрольныеТочки
	|		ПО СогласованиеТехническихПроектов.ТехническийПроект = ТехническиеПроектыКонтрольныеТочки.Ссылка
	|			И СогласованиеТехническихПроектов.КлючСтрокиКонтрольнойТочки = ТехническиеПроектыКонтрольныеТочки.КлючСтроки
	|ГДЕ
	|	(НЕ СогласованиеТехническихПроектов.ТехническийПроект.ПометкаУдаления
	|				И СогласованиеТехническихПроектов.Согласующий = &ТекущийПользователь
	|			ИЛИ СогласованиеТехническихПроектов.ТехническийПроект.Ответственный = &ТекущийПользователь)"
	;
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	ТекущаяДата = НачалоДня(Текущаядата());
	
	ДатаЗавтра = ОбщегоНазначенияСППР.СледующаяДатаПоОсновномуКалендарю();
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
	Запрос.УстановитьПараметр("ДатаЗавтра", ДатаЗавтра);
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("КоличествоСогласованияТехническихПроектов", 0);
	СтруктураРезультата.Вставить("КоличествоПодготовитьКСогласованиюТехническихПроектов", 0);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтруктураРезультата, Выборка);
	КонецЕслИ;
	
	Возврат СтруктураРезультата;
	
КонецФункции

#КонецОбласти

#КонецЕсли