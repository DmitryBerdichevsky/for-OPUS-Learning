#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Получение предыдущих данных набора для сравнения изменений
	
	ПредыдущиеДанныеНабора = РегистрыСведений.СогласованиеТехническихПроектов.СоздатьНаборЗаписей();
	
	ПредыдущиеДанныеНабора.Отбор.ТехническийПроект.Использование = Отбор.ТехническийПроект.Использование;
	ПредыдущиеДанныеНабора.Отбор.ТехническийПроект.Значение = Отбор.ТехническийПроект.Значение;
	
	ПредыдущиеДанныеНабора.Отбор.КлючСтрокиКонтрольнойТочки.Использование = Отбор.КлючСтрокиКонтрольнойТочки.Использование;
	ПредыдущиеДанныеНабора.Отбор.КлючСтрокиКонтрольнойТочки.Значение = Отбор.КлючСтрокиКонтрольнойТочки.Значение;
	
	ПредыдущиеДанныеНабора.Отбор.Согласующий.Использование = Отбор.Согласующий.Использование;
	ПредыдущиеДанныеНабора.Отбор.Согласующий.Значение = Отбор.Согласующий.Значение;
	
	ПредыдущиеДанныеНабора.Отбор.РольСогласующего.Использование = Отбор.РольСогласующего.Использование;
	ПредыдущиеДанныеНабора.Отбор.РольСогласующего.Значение = Отбор.РольСогласующего.Значение;
	
	ПредыдущиеДанныеНабора.Прочитать();
	ДополнительныеСвойства.Вставить("ПредыдущиеДанныеНабора", ПредыдущиеДанныеНабора.Выгрузить());
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдущиеДанныеНабора = Неопределено;
	Если ДополнительныеСвойства.Свойство("ПредыдущиеДанныеНабора") Тогда
		ПредыдущиеДанныеНабора = ДополнительныеСвойства.ПредыдущиеДанныеНабора;
	КонецЕсли;
	
	ЗаписатьДанныеВерсионирования(Выгрузить(),ПредыдущиеДанныеНабора);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаписатьДанныеВерсионирования(ТаблицаНабора, ПредыдущиеДанныеНабора)
	
	ТаблицаНабора = Выгрузить();
	ТаблицаНабора.Колонки.Добавить("БезИзменений", Новый ОписаниеТипов("Булево"));
	
	ОбъектыДляВерсионирования = Новый Массив;
	
	Если ПредыдущиеДанныеНабора <> Неопределено Тогда
		
		Для Каждого СтрокаТаблицы из ПредыдущиеДанныеНабора Цикл
			
			СтруктураОтбора = Новый Структура;
			
			Для Каждого Колонка из ПредыдущиеДанныеНабора.Колонки Цикл
				Если Колонка.Имя <> "Период" И Колонка.Имя <> "СрокОтработкиПоручения" Тогда 
					СтруктураОтбора.Вставить(Колонка.Имя, СтрокаТаблицы[Колонка.Имя]);
				КонецЕсли;
			КонецЦикла;
			
			МассивСтрок = ТаблицаНабора.НайтиСтроки(СтруктураОтбора);
			
			Если МассивСтрок.Количество() = 0 Тогда
				
				Если ОбъектыДляВерсионирования.Найти(СтрокаТаблицы.ТехническийПроект) = Неопределено Тогда
					ОбъектыДляВерсионирования.Добавить(СтрокаТаблицы.ТехническийПроект);
				КонецЕсли;
		
			Иначе
				Для Каждого СтрокаНабора из МассивСтрок Цикл
					СтрокаНабора.БезИзменений = Истина;
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура("БезИзменений", Ложь);
	МассивСтрокСИзменениями = ТаблицаНабора.НайтиСтроки(СтруктураОтбора);
	
	Для Каждого СтрокаНабора из МассивСтрокСИзменениями Цикл
		
		Если ОбъектыДляВерсионирования.Найти(СтрокаНабора.ТехническийПроект) = Неопределено Тогда
			ОбъектыДляВерсионирования.Добавить(СтрокаНабора.ТехническийПроект);
		КонецЕсли;	
		
	КонецЦикла;
	
	Для Каждого ОбъектДляВерсионирования из ОбъектыДляВерсионирования Цикл
		
		ЗаписываемыйОбъект = ОбъектДляВерсионирования.ПолучитьОбъект();
		
		ЗаписываемыйОбъект.ДополнительныеСвойства.Вставить("КоллекцияИзмененныхОбъектов", Новый Массив);
		Версионирование.ЗарегистрироватьИзмененияОбъекта(ЗаписываемыйОбъект);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли