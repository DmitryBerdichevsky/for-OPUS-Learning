
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ОписаниеТиповОтвета = ОписаниеТиповОтвета(ТекущийОбъект.Вопрос);
	
	УстановитьСтраницуОтвета(ТипЗнч(ТекущийОбъект.Ответ), Элементы);
	УстановитьДоступностьРедактированияТипаОтвета(Элементы, ОписаниеТиповОтвета);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_РезультатыОтветовНаВопросыТехническихПроектов");
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтветПриИзменении(Элемент)
	
	УстановитьСтраницуОтвета(ТипЗнч(Запись.Ответ), Элементы);
	УстановитьДоступностьРедактированияТипаОтвета(Элементы, ОписаниеТиповОтвета);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ОписаниеТиповОтвета(Вопрос)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВопросыПоТехническимПроектам.ТипЗначения
	|ИЗ
	|	ПланВидовХарактеристик.ВопросыПоТехническимПроектам КАК ВопросыПоТехническимПроектам
	|ГДЕ
	|	ВопросыПоТехническимПроектам.Ссылка = &Ссылка"
	;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Вопрос);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат Новый ОписаниеТипов("Неопределено");
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ТипЗначения;
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСтраницуОтвета(ТипОтвета, Элементы)
	
	Если ТипОтвета = Тип("Строка") Тогда
		Элементы.ГруппаСтраницыОтвета.ТекущаяСтраница = Элементы.СтраницаОтветСтрокой;
	Иначе
		Элементы.ГруппаСтраницыОтвета.ТекущаяСтраница = Элементы.СтраницаОтвет;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьРедактированияТипаОтвета(Элементы, ОписаниеТиповОтвета)
	
	Элементы.ОтветСтрокой.ВыбиратьТип = ОписаниеТиповОтвета.Типы().Количество()>1;
	Элементы.Ответ.ВыбиратьТип = ОписаниеТиповОтвета.Типы().Количество()>1;
	
КонецПроцедуры

#КонецОбласти
