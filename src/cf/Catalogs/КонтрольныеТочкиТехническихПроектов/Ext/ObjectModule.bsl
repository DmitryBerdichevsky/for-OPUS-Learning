#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
			Если ДанныеЗаполнения.Свойство("Владелец") Тогда
				Владелец = ДанныеЗаполнения.Владелец;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Владелец) Тогда
			Владелец = Проекты.ПроектПоУмолчанию();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьСправочников.УстановитьКодСправочника(ЭтотОбъект);
	
	ПроверитьНаличиеКонтрольныхТочекПоКоду(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьНаличиеКонтрольныхТочекПоКоду(Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекстЗапроса=
	"ВЫБРАТЬ
	|	КонтрольныеТочки.Ссылка КАК КонтрольнаяТочка
	|ИЗ
	|	Справочник.КонтрольныеТочкиТехническихПроектов КАК КонтрольныеТочки
	|ГДЕ
	|	КонтрольныеТочки.Код = &Код
	|	И КонтрольныеТочки.Владелец = &Проект
	|	И КонтрольныеТочки.Ссылка <> &Ссылка"
	;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Код", Код);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Проект", Владелец);
	
	Запрос.Текст = ТекстЗапроса;
	
	Если НЕ Запрос.Выполнить().Пустой() Тогда
		ТекстСообщения = НСтр("ru='В информационной базе уже существует контрольная точка с кодом %Код%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Код%", Код);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Код");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли