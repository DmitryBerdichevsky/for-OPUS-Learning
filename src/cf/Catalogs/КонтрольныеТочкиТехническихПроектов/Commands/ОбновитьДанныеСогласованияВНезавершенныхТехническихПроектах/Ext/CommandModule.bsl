
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если Не ЗначениеЗаполнено(ПараметрКоманды) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗаголовка = НСтр("ru='Обновление данных согласования'");
	ТекстСостояния = НСтр("ru='Выполняется обновление данных согласования технических проектов'");
	Состояние(ТекстЗаголовка, ТекстСостояния);
	
	ОбновитьДанныеСервер(ПараметрКоманды);
	
	ЗаголовокСообщения = НСтр("ru='Обновление данных согласования'");
	Пояснение = НСтр("ru='Данные согласования технических проектов обновлены'");
	ПоказатьОповещениеПользователя(ЗаголовокСообщения,, Пояснение);
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДанныеСервер(КонтрольнаяТочка)
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТехническиеПроектыКонтрольныеТочки.Ссылка КАК ТехническийПроект,
	|	ТехническиеПроектыКонтрольныеТочки.КлючСтроки КАК КлючСтрокиКонтрольнойТочки,
	|	КонтрольныеТочкиТехническихПроектовШаблонСогласования.Согласующий КАК Согласующий,
	|	КонтрольныеТочкиТехническихПроектовШаблонСогласования.РольСогласующего КАК РольСогласующего,
	|	ТехническиеПроектыКонтрольныеТочки.КонтрольнаяТочка КАК КонтрольнаяТочка
	|ИЗ
	|	Справочник.ТехническиеПроекты.КонтрольныеТочки КАК ТехническиеПроектыКонтрольныеТочки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтрольныеТочкиТехническихПроектов.ШаблонСогласования КАК КонтрольныеТочкиТехническихПроектовШаблонСогласования
	|		ПО ТехническиеПроектыКонтрольныеТочки.КонтрольнаяТочка = КонтрольныеТочкиТехническихПроектовШаблонСогласования.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СогласованиеТехническихПроектов.СрезПоследних КАК СогласованиеТехническихПроектов
	|		ПО ТехническиеПроектыКонтрольныеТочки.Ссылка = СогласованиеТехническихПроектов.ТехническийПроект
	|			И ТехническиеПроектыКонтрольныеТочки.КлючСтроки = СогласованиеТехническихПроектов.КлючСтрокиКонтрольнойТочки
	|ГДЕ
	|	ТехническиеПроектыКонтрольныеТочки.КонтрольнаяТочка = &КонтрольнаяТочка
	|	И ТехническиеПроектыКонтрольныеТочки.Ссылка.Статус В(&МассивСтатусов)
	|	И ТехническиеПроектыКонтрольныеТочки.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыКонтрольныхТочекТехническихПроектов.Назначена)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТехническийПроект,
	|	КонтрольнаяТочка,
	|	Согласующий"
	;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("КонтрольнаяТочка", КонтрольнаяТочка);
	
	МассивСтатусов = Новый Массив;
	МассивСтатусов.Добавить(Перечисления.СтатусыТехническихПроектов.Активен);
	МассивСтатусов.Добавить(Перечисления.СтатусыТехническихПроектов.НеЗапланирован);
	МассивСтатусов.Добавить(Перечисления.СтатусыТехническихПроектов.Запланирован);
	
	Запрос.УстановитьПараметр("МассивСтатусов", МассивСтатусов);
	
	ТехническийПроект = Неопределено;
	НаборЗаписей = Неопределено;
	ТаблицаНабора = Неопределено;
	НоваяТаблицаНабора = Неопределено;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ТехническийПроект <> ТехническийПроект Тогда
			
			ТехническийПроект = Выборка.ТехническийПроект;
			Если НаборЗаписей <> Неопределено Тогда
				Если НоваяТаблицаНабора.Количество()>0 Тогда
					НаборЗаписей.Загрузить(НоваяТаблицаНабора);
					НаборЗаписей.Записать();
				КонецЕсли;
			КонецЕсли;
			
			НаборЗаписей = РегистрыСведений.СогласованиеТехническихПроектов.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ТехническийПроект.Установить(Выборка.ТехническийПроект, Истина);
			НаборЗаписей.Отбор.КлючСтрокиКонтрольнойТочки.Установить(Выборка.КлючСтрокиКонтрольнойТочки, Истина);
			НаборЗаписей.Прочитать();
			
			ТаблицаНабора = НаборЗаписей.Выгрузить();
			
			НаборЗаписей.Очистить();
			
			НоваяТаблицаНабора = НаборЗаписей.Выгрузить();
			
		КонецЕсли;
		
		// Если информация о согласовании имеется, то при наличии согласования записи остаются всегда,
		// а в остальных случаях - если в шаблоне по-прежнему есть согласующий с указанной ролью. 
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("КлючСтрокиКонтрольнойТочки", Выборка.КлючСтрокиКонтрольнойТочки);
		
		МассивСтрок = ТаблицаНабора.НайтиСтроки(СтруктураОтбора);
		
		Для Каждого СтрокаЗаписи из МассивСтрок Цикл
			
			Если СтрокаЗаписи.СтатусСогласования = Перечисления.СтатусыСогласованияТехническихПроектов.Согласовано
				ИЛИ СтрокаЗаписи.СтатусСогласования = Перечисления.СтатусыСогласованияТехническихПроектов.НеСогласовано
				ИЛИ Выборка.Согласующий = СтрокаЗаписи.Согласующий И Выборка.РольСогласующего = СтрокаЗаписи.РольСогласующего Тогда
				
				СтруктураОтбораСтрок = Новый Структура;
				СтруктураОтбораСтрок.Вставить("Период", СтрокаЗаписи.Период);
				СтруктураОтбораСтрок.Вставить("КлючСтрокиКонтрольнойТочки", СтрокаЗаписи.КлючСтрокиКонтрольнойТочки);
				СтруктураОтбораСтрок.Вставить("Согласующий", СтрокаЗаписи.Согласующий);
				СтруктураОтбораСтрок.Вставить("РольСогласующего", СтрокаЗаписи.РольСогласующего);
				
				НайденныеСтроки = НоваяТаблицаНабора.НайтиСтроки(СтруктураОтбораСтрок);
				
				Если НайденныеСтроки.Количество()=0 Тогда
					НоваяСтрока = НоваяТаблицаНабора.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗаписи);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("КлючСтрокиКонтрольнойТочки", Выборка.КлючСтрокиКонтрольнойТочки);
		СтруктураОтбора.Вставить("Согласующий", Выборка.Согласующий);
		СтруктураОтбора.Вставить("РольСогласующего", Выборка.РольСогласующего);
		
		МассивСтрок = НоваяТаблицаНабора.НайтиСтроки(СтруктураОтбора);
		
		Если МассивСтрок.Количество()=0 Тогда
			// Если информации о согласованиии еще нет, она просто добавляется.
			НоваяСтрока = НоваяТаблицаНабора.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.Период = ТекущаяДата();
			НоваяСтрока.СтатусСогласования = Перечисления.СтатусыСогласованияТехническихПроектов.НеГотовоКСогласованию;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти