#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрКоманды = Неопределено Тогда
		Возврат;
	ИначеЕсли ТипЗнч(ПараметрКоманды) = Тип("Массив") и ПараметрКоманды.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	СообщенияПользователю = Неопределено;
	ОтметитьПрохождениеКонтрольнойТочки(ПараметрКоманды, СообщенияПользователю);
	
	Если ТипЗнч(СообщенияПользователю) = Тип("ФиксированныйМассив") Тогда
		Для Каждого Сообщение из СообщенияПользователю Цикл
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтметитьПрохождениеКонтрольнойТочки(МассивТехническихПроектов, Сообщения)

	ПолучитьСообщенияПользователю(Истина);
	
	Для Каждого ТехническийПроект из МассивТехническихПроектов Цикл
		
		Объект = ТехническийПроект.ПолучитьОбъект();
		
		Если Не Объект.ИспользуютсяКонтрольныеТочкиПроекта Тогда
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Для технического проекта ""%1"" не используются контрольные точки.'"), Объект.Наименование);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ТехническийПроект);
			Продолжить;
			
		КонецЕсли;
		
		Попытка
			Объект.Заблокировать();
		Исключение
			ТекстСообщения = НСтр(" ru= ""Не удалось отметить прохождение контрольной точки для технического проекта: %Проект% по причине: %ОписаниеОшибки%""; ");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Проект%", ТехническийПроект);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", ОписаниеОшибки());
			
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = ТекстСообщения;
			СообщениеПользователю.Сообщить();
			Продолжить;
		КонецПопытки;
		
		Попытка
			
			
			Для Каждого СтрокаТЧ из Объект.КонтрольныеТочки Цикл
				Если СтрокаТЧ.Статус = Перечисления.СтатусыКонтрольныхТочекТехническихПроектов.Назначена Тогда
					СтрокаТЧ.Статус = Перечисления.СтатусыКонтрольныхТочекТехническихПроектов.Пройдена;
					СтрокаТЧ.СрокПрохождения = ТекущаяДата();
					ТехническиеПроекты.ПересчитатьСрокиИДлительностьКонтрольныхТочекСервер(СтрокаТЧ, "СрокПрохождения", Объект);
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Объект.ИзменитьСтатусПриНеобходимости();
			Объект.Записать();
			Объект.Разблокировать();
		Исключение
			ТекстСообщения = НСтр(" ru= ""Не удалось отметить прохождение контрольной точки для технического проекта: %Проект% по причине: %ОписаниеОшибки%""; "); 
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Проект%", ТехническийПроект);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", ОписаниеОшибки());
			
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = ТекстСообщения;
			СообщениеПользователю.Сообщить();
		КонецПопытки;			
		
	КонецЦикла;

	Сообщения = ПолучитьСообщенияПользователю(Истина);
	
КонецПроцедуры

#КонецОбласти